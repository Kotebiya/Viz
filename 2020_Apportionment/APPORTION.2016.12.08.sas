%LET POPEST=\Directory\;
LIBNAME POPEST "&POPEST.";

LIBNAME POPXL PCFILES PATH="&POPEST.POPEST.XLSX" ACCESS=READONLY;
DATA POPEST.POPEST;
	SET POPXL.'DATA$'n;
RUN;
LIBNAME POPXL CLEAR;

PROC TRANSPOSE DATA=POPEST.POPEST(WHERE=(SUMLEVEL="040" & APPORTIONMENT = 1)) OUT=POPEST.POPEST2(DROP=_LABEL_ RENAME=(_NAME_=NAMEYR COL1=POP));
	BY APPORTIONMENT ST State CEN2000 CEN2010;
RUN;

DATA POPEST.POPEST3;
	RETAIN INDEX ST State YEAR APPORTIONMENT CEN2000 CEN2010;
	MERGE POPEST.POPEST2 POPEST.POPEST2(FIRSTOBS=2 RENAME=(POP=NEXTPOP) KEEP=POP);
	YEAR = INPUT(SUBSTR(NAMEYR,4,4),BEST4.); DROP NAMEYR;
	FORMAT GROW1 7.5;
	IF YEAR = 2015 THEN NEXTPOP = .;
		ELSE GROW1 = LOG(NEXTPOP) / LOG(POP);
	IF ST NE LAG1(ST) THEN INDEX + 1;
RUN;

PROC SQL;
	CREATE TABLE POPEST.SUMPOP AS
	SELECT
	ST, State,
	MEAN(GROW1) FORMAT=7.5 AS AVG_GROW, STD(GROW1) FORMAT=7.5 AS STD_GROW

	FROM POPEST.POPEST3(WHERE=(YEAR NE 2015))
	GROUP BY ST, State;
QUIT;

PROC SQL;
	CREATE TABLE POPEST.POPEST4 AS
	SELECT
	POP.ST, POP.STATE FORMAT=$14. AS STATE, POP.INDEX, POP.CEN2010, POP.YEAR FORMAT=4. AS YEAR,
	POP.POP FORMAT=COMMA12. AS POP,
	LOG(POP.POP) FORMAT=7.4 AS START,
	GROW.AVG_GROW AS GROW, GROW.STD_GROW AS GROW_SE,
	LOG(POP**(GROW**4.75)) FORMAT=7.4 AS END,
	(LOG(POP**(GROW+GROW_SE)**4.75)-LOG(POP**(GROW-GROW_SE)**4.75))/2
		FORMAT=7.4 AS END_SE, CALCULATED END_SE**2 FORMAT=7.4 AS END_VAR,
	POP**(GROW-GROW_SE*1.96)**4.75 FORMAT=COMMA12. AS LOW,
	POP**(GROW+GROW_SE*1.96)**4.75 FORMAT=COMMA12. AS HIGH,
	EXP(CALCULATED END) FORMAT=COMMA12. AS NEWPOP

	FROM POPEST.POPEST3/*(WHERE=(YEAR=2015))*/ AS POP
	LEFT JOIN POPEST.SUMPOP AS GROW
	ON POP.ST = GROW.ST
	ORDER BY POP.ST, POP.YEAR;
QUIT;

PROC SQL;
	CREATE TABLE POPEST.USA AS
	SELECT
	SUM(CEN2010) FORMAT=COMMA12. AS USA10, SUM(NEWPOP) FORMAT=COMMA12. AS USA20
	FROM POPEST.POPEST4(WHERE=(YEAR=2015));
QUIT;

PROC SQL;
	CREATE TABLE POPEST.POPEST5 AS
	SELECT *
	FROM	POPEST.POPEST4(WHERE=(YEAR=2015)), POPEST.USA;
QUIT;

DATA _NULL_;
	SET POPEST.POPEST5;
	FORMAT END END_VAR 10.7;
	PUT INDEX END END_VAR;
RUN;

DATA PRIORS;
input _type_ $ Intercept INDEX1 INDEX2 INDEX3 INDEX4 INDEX5 INDEX6 INDEX7 INDEX8 INDEX9 INDEX10 INDEX11 INDEX12 INDEX13 INDEX14 INDEX15 INDEX16 INDEX17 INDEX18 INDEX19 INDEX20 INDEX21 INDEX22 INDEX23 INDEX24 INDEX25 INDEX26 INDEX27 INDEX28 INDEX29 INDEX30 INDEX31 INDEX32 INDEX33 INDEX34 INDEX35 INDEX36 INDEX37 INDEX38 INDEX39 INDEX40 INDEX41 INDEX42 INDEX43 INDEX44 INDEX45 INDEX46 INDEX47 INDEX48 INDEX49 INDEX50;
DATALINES;
MEAN 1 15.4241324518277 13.5640133782530 15.8262772726699 14.9405671518743 17.5277513441027 15.5865206525865 15.1101492665001 13.8189534932415 16.8993922687873 16.2084905971149 14.2270479046026 14.3967188881002 16.3803101199210 15.7319626268704 14.9750435793772 14.9089526046096 15.3310352886733 15.3706757204170 14.1129090312667 15.6475029230651 15.7525401395330 16.1093723206814 15.5523284595328 14.9272146164848 15.6470302856596 13.8905053545323 14.4875276723991 14.9926326689923 14.1235859703241 16.0273222614367 14.5934536207313 16.8139647905305 16.1917872760572 13.5895981154836 16.2745661777563 15.2189475647159 15.2603778800911 16.3782608345000 13.8720971211582 15.4665903886865 13.7034732280522 15.7491439933697 17.2153554111072 15.0053914549154 13.3556029266557 15.9944186894868 15.8471507938010 14.4339609638855 15.5910713670769 13.3356746258997
VAR 1 0.00023665596443692 0.00069333762449115 0.00136652268165501 0.00025759828538083 0.00010280519016085 0.00029084313895239 0.00015620384947649 0.00019889083464739 0.00065763834033907 0.00055695932628012 0.00022335478966269 0.00094614612865016 0.00007029379851597 0.00005782829343255 0.00006529871643774 0.00012302335569764 0.00010377491007939 0.00765259615049025 0.00023264129724507 0.00012472469648117 0.00024353253637468 0.00017051980021619 0.00003020809808647 0.00015572594651555 0.00009507943014052 0.00017422597699739 0.00004780614340641 0.00383335125173271 0.00035783499130545 0.00007735491586529 0.00078358121418476 0.00013311697381861 0.00054687510930308 0.00245617757101714 0.00000714242969427 0.00018715373939061 0.00019524927385212 0.00004261477974872 0.00039818787616731 0.00034667534001700 0.00023219095805510 0.00019527084220094 0.00014079435077912 0.00052799812743478 0.00007735085768663 0.00011681765414675 0.00013570914300241 0.00015265008245357 0.00005908716209114 0.00130448136741299
;
RUN;

ODS EXCLUDE ALL;
PROC GENMOD DATA=POPEST.POPEST;
	CLASS INDEX:;
	MODEL END = INDEX: / DIST=NORMAL NOINT;
	BAYES SEED=1776 NMC=1000000 OUTPOST=POPEST.POP_GENNY COEFF=NORMAL(INPUT=PRIORS);
RUN; QUIT;
ODS EXCLUDE NONE;

DATA POPEST.POP_GENNY2;
	SET POPEST.POP_GENNY;
	ARRAY STATES(50) INDEX1-INDEX50;
	ARRAY ST_CD(50) ST_CD1-ST_CD50;
	ARRAY ST_CDE(50) ST_CDE1-ST_CDE50;
	ARRAY EXTRA(15) CD436-CD450;
	DO CD = 1 TO 450;
		IF (CD) IN(1:50) THEN DO;
			ST_CD(CD) = 1;
			ST_CDE(CD) = 0;
		END;
		IF (CD) IN(51:435) THEN DO;
			DO ST = 1 TO 50;
				IF (ST) = 1 THEN MAXPOP = 0;
				MAXPOP = MAX(EXP(STATES(ST))/(((ST_CD(ST)+1)*(ST_CD(ST)))**.5), MAXPOP);
				IF MAXPOP = EXP(STATES(ST))/(((ST_CD(ST)+1)*(ST_CD(ST)))**.5) THEN CONTENDER = (ST);
			END;
			DO ST = 1 TO 50;
				IF CONTENDER = (ST) THEN ST_CD(ST) + 1;
			END;
		END;
		IF (CD) IN(436:450) THEN DO;
			DO ST = 1 TO 50;
				IF (ST) = 1 THEN MAXPOP = 0;
				MAXPOP = MAX(EXP(STATES(ST))/(((ST_CD(ST)+ST_CDE(ST)+1)*(ST_CD(ST)+ST_CDE(ST)))**.5), MAXPOP);
				IF MAXPOP = EXP(STATES(ST))/(((ST_CD(ST)+ST_CDE(ST)+1)*(ST_CD(ST)+ST_CDE(ST)))**.5) THEN CONTENDER = (ST);
			END;
			DO ST = 1 TO 50;
				IF CONTENDER = (ST) THEN EXTRA(CD-435) = (ST);
				IF CONTENDER = (ST) THEN ST_CDE(ST) + 1;
			END;
		END;
	END;
	CONSTANT = 1;
RUN;

PROC SUMMARY DATA=POPEST.POP_GENNY2 NWAY;
	CLASS ST_CD1--ST_CD50;
	VAR CONSTANT;
	OUTPUT OUT=POPEST.APPORTION(DROP=_:) SUM=SCENARIOS;
RUN;
QUIT;

%LET APPORTION=/Directory/;
LIBNAME APPORT "&APPORTION.";
LIBNAME APP_XL PCFILES PATH="&APPORTION.STATE_CD.xlsx" ACCESS=READONLY;
DATA POPEST.CD10;
	FORMAT INDEX BEST2. NAME $14. FIPS $2. CD BEST2.;
	SET APP_XL.'CD10$'n;
RUN;
LIBNAME APP_XL CLEAR;

DATA _NULL_;
	SET POPEST.CD10 END=EOF;
	IF _n_ = 1 THEN CALL EXECUTE("
		DATA POPEST.EXTRA_CD;
			SET POPEST.EXTRA;
			ARRAY INDEXER(*) CD436-CD450;
			ARRAY INDEX2(*) CDST436-CDST450;
			FORMAT CDST436-CDST450 $2.;
			DO i = 1 TO DIM(INDEXER);
	");
	CALL EXECUTE("
				IF INDEXER(i) = "||COMPRESS(INDEX)||" THEN INDEX2(i) = "||COMPRESS(FIPS)||";
	");
	IF EOF THEN CALL EXECUTE("
			END;
			DROP i CD436--CD450;
		RUN;
	");
RUN;

PROC SORT DATA=POPEST.EXTRA_CD;
	BY DESCENDING SCENARIOS;
RUN;

PROC SORT DATA=POPEST.APPORTION;
	BY DESCENDING SCENARIOS;
RUN;

DATA _NULL_;
	SET POPEST.CD10 END=EOF;
	IF _n_ = 1 THEN CALL EXECUTE("
		PROC SQL;
			CREATE TABLE POPEST.APPORTION2 AS
			SELECT FIPS, NAME, CD10 FORMAT=BEST2. AS CD10, CD FORMAT=BEST2. AS CD,
			CD - CD10 FORMAT=BEST2. AS CHANGE,
			SCENARIOS, (SCENARIOS/1000000)*100 FORMAT=6.2 AS SHARE
			FROM (
	");
	IF NOT EOF THEN CALL EXECUTE("
		SELECT 
			"""||COMPRESS(FIPS)||""" AS FIPS , """||TRIM(NAME)||""" AS NAME, "||TRIM(CD)||" AS CD10, ST_CD"||COMPRESS(INDEX)||" AS CD, SUM(SCENARIOS) AS SCENARIOS
			FROM POPEST.APPORTION
			GROUP BY ST_CD"||COMPRESS(INDEX)||"
			OUTER UNION CORRESPONDING
	");
	IF EOF THEN CALL EXECUTE("
		SELECT 
				""56"" AS FIPS, ""Wyoming"" AS NAME, 1 AS CD10, ST_CD50 AS CD, SUM(SCENARIOS) AS SCENARIOS
				FROM POPEST.APPORTION
				GROUP BY ST_CD50);
		QUIT;
	");
RUN;

DATA POPEST.APPORTION3;
	SET POPEST.APPORTION2;
	IF CHANGE = -3 THEN TXT = "_3";
		ELSE IF CHANGE = -2 THEN TXT = "_2";
		ELSE IF CHANGE = -1 THEN TXT = "_1";
		ELSE IF CHANGE = 0 THEN TXT = "0";
		ELSE IF CHANGE = 1 THEN TXT = "1";
		ELSE IF CHANGE = 2 THEN TXT = "2";
		ELSE IF CHANGE = 3 THEN TXT = "3";
		ELSE IF CHANGE = 4 THEN TXT = "4";
		ELSE IF CHANGE = 5 THEN TXT = "5";
RUN;

PROC TRANSPOSE DATA=POPEST.APPORTION3 OUT=POPEST.APPORTION4 PREFIX=SCENE;
	BY FIPS NAME CD10;
	ID CHANGE;
	VAR SCENARIOS;
RUN;

DATA POPEST.APPORTION4(RENAME=(SCENE_3=MINUS3 SCENE_2=MINUS2 SCENE_1=MINUS1 SCENE0=SAME SCENE1=PLUS1 SCENE2=PLUS2 SCENE3=PLUS3 SCENE4=PLUS4 SCENE5=PLUS5));
	FORMAT FIPS $2. NAME $14. CD10 BEST2. SCENE_3 SCENE_2 SCENE_1 SCENE0 SCENE1 SCENE2 SCENE3 SCENE4 SCENE5 BEST7.;
	SET POPEST.APPORTION4;
	DROP _NAME_;
	ARRAY CHANGE(9) SCENE_3 SCENE_2 SCENE_1 SCENE0 SCENE1 SCENE2 SCENE3 SCENE4 SCENE5;
	DO i = 1 TO 9;
		IF CHANGE(i) = . THEN CHANGE(i) = 0;
	END;
	DROP i;
RUN;

DATA _NULL_;
	SET POPEST.APPORTION4;
	FILE "&APPORTION.APPORTIONMENT.CSV"  DLM="," DSD NOTITLES;
		IF _n_ = 1 THEN PUT "FIPS,NAME,CD10,MINUS3,MINUS2,MINUS1,SAME,PLUS1,PLUS2,PLUS3,PLUS4,PLUS5";
	PUT FIPS NAME CD10 MINUS3 MINUS2 MINUS1 SAME PLUS1 PLUS2 PLUS3 PLUS4 PLUS5;
RUN;

DATA _NULL_;
	SET POPEST.APPORTION3;
	FILE "&APPORTION.APPORTION_SCENARIOS.CSV" DLM="," DSD NOTITLES;
		IF _n_ = 1 THEN PUT "FIPS,NAME,CD10,CHANGE,SCENARIOS";
	PUT FIPS NAME CD10 CHANGE SCENARIOS;
RUN;

DATA _NULL_;
	FORMAT POP LOW HIGH NEWPOP 12.;
	SET POPEST.POPEST5;
	FILE "&APPORTION.POP_ESTIMATES.CSV"  DLM="," DSD NOTITLES;
		IF _n_ = 1 THEN PUT "ST,STATE,YEAR,CEN10,POP,LOW,HIGH,PRED,USA10,USA20";
		PUT ST STATE YEAR CEN2010 POP LOW HIGH NEWPOP USA10 USA20;
RUN;
