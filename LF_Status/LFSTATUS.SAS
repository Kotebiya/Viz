%LET BASIC=/folders/myshortcuts/DATA/Census/CPS Basic/;
LIBNAME BASIC "&BASIC.";

%LET LFSTATUS=/folders/myshortcuts/DATA/Viz/LFSTATUS/;
LIBNAME LFSTATUS "&LFSTATUS.";

DATA FREQ2B;
	SET FREQ;
	HHID = TRIM(HRHHID||HRHHID2);
	ID = TRIM(HHID||INPUT(PUT(PULINENO,z2.),$2.));

	/* Race Variable */
	RACE = .;
	IF PRHSPNON = 2 & PERACE = 1 THEN RACE = 1;
		ELSE IF PRHSPNON = 2 & PERACE = 2 THEN RACE = 2;
		ELSE IF PRHSPNON = 2 & PERACE = 4 THEN RACE = 3;
		ELSE IF PRHSPNON = 2 & PERACE = 3 THEN RACE = 4;
		ELSE IF PRHSPNON = 1 THEN RACE = 5;
	IF substr(put(HRYEAR4,4.),1,2) = "20" THEN DO;
		IF PEHSPNON = 2 & PTDTRACE = 1 THEN RACE = 1;
			ELSE IF PEHSPNON = 2 & PTDTRACE = 2 THEN RACE = 2;
			ELSE IF PEHSPNON = 2 & PTDTRACE IN(3,4) THEN RACE = 3;
			ELSE IF PEHSPNON = 2 & PTDTRACE NOTIN(1:4) THEN RACE = 4;
			ELSE IF PEHSPNON = 1 THEN RACE = 5;
	END;

	/* Educational Attainment */
	EDUC = .;
	IF PEEDUCA IN(31:38) THEN EDUC = 1; /* Less than High School */
		ELSE IF PEEDUCA IN(39) THEN EDUC = 2; /* High School Graduate */
		ELSE IF PEEDUCA IN(40:42) THEN EDUC = 3; /* Some College Experience */
		ELSE IF PEEDUCA IN(43:46) THEN EDUC = 4; /* Bachelor's, Master's, or Professional Degree */

	/* Labor Force Status */
	IF PEMLR in(1,2) & (50 <= PEHRUSLT <= 198) THEN LFSTATUS = 1; /* Full-time */
		ELSE IF PEMLR in(1,2) & ((36 <= PEHRUSLT <= 49) | (PEHRFTPT = 1)) THEN LFSTATUS = 2; /* Full-time */
		ELSE IF PEMLR in(1,2) & ((0 <= PEHRUSLT <= 35) | (PEHRFTPT in(1,2))) THEN LFSTATUS = 3; /* Part-time */
		ELSE IF PEMLR in(3,4) THEN LFSTATUS = 4; /* Unemployed */
		ELSE IF PEMLR in(5,6,7) & PENLFACT = 4 THEN LFSTATUS = 5; /* Taking care of house/family */
		ELSE IF PEMLR in(5,6,7) & ((PESCHENR = 1) | (PEDWAVR = 2)) THEN LFSTATUS = 6; /* In School */
		ELSE IF PEMLR = 5 THEN LFSTATUS = 7; /* Retired */
		ELSE IF PEMLR = 6 THEN LFSTATUS = 9; /* With a disability */
		ELSE LFSTATUS = 8; /* NILF - Other reason */

	PWGT = PWSSWGT;
	MON = INPUT(PUT(HRMONTH,z2.),$2.);
	
	IF PRTAGE IN(16:17) OR PEAGE IN(16:17) THEN AGECAT = 16;
		ELSE IF PRTAGE IN(18:24) OR PEAGE IN(18:24) THEN AGECAT = 18;
		ELSE IF PRTAGE IN(25:29) OR PEAGE IN(25:29) THEN AGECAT = 25;
		ELSE IF PRTAGE IN(30:34) OR PEAGE IN(30:34) THEN AGECAT = 30;
		ELSE IF PRTAGE IN(35:39) OR PEAGE IN(35:39) THEN AGECAT = 35;
		ELSE IF PRTAGE IN(40:44) OR PEAGE IN(40:44) THEN AGECAT = 40;
		ELSE IF PRTAGE IN(45:49) OR PEAGE IN(45:49) THEN AGECAT = 45;
		ELSE IF PRTAGE IN(50:54) OR PEAGE IN(50:54) THEN AGECAT = 50;
		ELSE IF PRTAGE IN(55:59) OR PEAGE IN(55:59) THEN AGECAT = 55;
		ELSE IF PRTAGE IN(60:64) OR PEAGE IN(60:64) THEN AGECAT = 60;
		ELSE IF PRTAGE IN(65:69) OR PEAGE IN(65:69) THEN AGECAT = 65;
		ELSE IF PRTAGE IN(70:74) OR PEAGE IN(70:74) THEN AGECAT = 70;
		ELSE IF PRTAGE IN(75:99) OR PEAGE IN(75:99) THEN AGECAT = 75;
	IF PRTAGE NE . THEN AGE = PRTAGE;
		ELSE IF PEAGE NE . THEN AGE = PEAGE;

	DROP HRHHID HRHHID2 PWSSWGT
		HRMONTH PTDTRACE PEHSPNON PERACE PRHSPNON PEEDUCA
		PESCHENR PEDWAVR PENLFACT PEHRUSLT PEHRFTPT;
	RENAME	PESEX = SEX
			HRYEAR4 = YEAR
			HRMIS = MIS;
RUN;

PROC FREQ DATA=FREQ2B;
	*TABLES YEAR AGE SEX LFSTATUS MIS;
	TABLES YEAR*AGECAT / NOROW NOCOL NOPERCENT NOCUM;
RUN;

/* If RACE is included: YEAR IN (2006:2015) */
PROC SUMMARY DATA=FREQ2B NWAY;
	CLASS YEAR SEX LFSTATUS AGECAT;
	VAR PWGT;
	OUTPUT OUT=LFSTATUS.LFSTATUS(DROP=_:) SUM=POP;
	WHERE AGE IN(16:99) & MIS = 1 & YEAR IN(2000:2015);
RUN;

PROC EXPORT DATA=LFSTATUS.LFSTATUS OUTFILE="&LFSTATUS.LIFECOURSE.XLS" DBMS=XLS REPLACE;
RUN;

DATA LFSTATUS.LFNUM(DROP=FILLER);
	INPUT TYPE 1. FILLER $1. Title $19.;
CARDS;
1	Over-time
2	Full-time
3	Part-time
4	Unemployed
5	Taking care of home
6	School
7	Retired
8	Other
9	Disabled
;
RUN;

DATA LFSTATUS.AGECAT(DROP=FILLER);
	INPUT AGECAT 2. FILLER $1. RANGE $8.;
CARDS;
16 16 to 17
18 18 to 24
25 25 to 29
30 30 to 34
35 35 to 39
40 40 to 44
45 45 to 49
50 50 to 54
55 55 to 59
60 60 to 64
65 65 to 69
70 70 to 74
75 75 plus
;
RUN;