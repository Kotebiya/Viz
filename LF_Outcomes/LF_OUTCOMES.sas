/* Section 1: Set Parameters */;
%LET BASIC=/DATA/Census/CPS Basic/;
LIBNAME BASIC "&BASIC.";

%LET LFSTATUS=/DATA/Viz/LF_STATUS/;
LIBNAME LFSTATUS "&LFSTATUS.";

%LET OUTCOME=/data/Viz/LF_OUTCOMES/;
LIBNAME OUTCOME "&OUTCOME.";

%LET DATA_ORDER=HRHHID HRHHID2 PULINENO HRYEAR4 HRMONTH HRMIS
	PUCHINHH HWHHWGT PWSSWGT
	PESEX PRTAGE PEAGE PEMARITL PRCITSHP
	PERACE PRHSPNON PTDTRACE PEHSPNON
	PERRP HEFAMINC
	PEEDUCA PESCHENR PEDWAVR
	PEMLR PEHRUSLT PEHRFTPT PELKDUR PENLFACT
	HETENURE HEHOUSUT;
/* Section 2 & 3: Pulling the Data */;
DATA OUTCOME.OUTCOME1;
	SET BASIC;
	/* CREATES WORKING UNIQUE PERSON ID */
	IF HRMIS IN(1:4) THEN IDAGE = MAX(OF PEAGE PRTAGE);
		ELSE IF HRMIS IN(5:8) THEN IDAGE = MAX(OF PEAGE PRTAGE) - 1;
	LENGTH KEYID $25.;
	KEYID = COMPRESS(HRHHID||HRHHID2||INPUT(PUT(PULINENO,z2.),$2.)||INPUT(PESEX,$1.)||INPUT(PUT(IDAGE,z2.),$2.));
	/* Race Variable */
	RACE = .; /* 1 - NHW 2 - NHB 3 - NHA 4 - NHO 5 - HISP */
	IF PRHSPNON = 2 & PERACE = 1 THEN RACE = 1;
		ELSE IF PRHSPNON = 2 & PERACE = 2 THEN RACE = 2;
		ELSE IF PRHSPNON = 2 & PERACE = 4 THEN RACE = 3;
		ELSE IF PRHSPNON = 2 & PERACE = 3 THEN RACE = 4;
		ELSE IF PRHSPNON = 1 THEN RACE = 5;
	IF substr(put(HRYEAR4,4.),1,2) = "20" THEN DO;
		IF PEHSPNON = 2 & PTDTRACE = 1 THEN RACE = 1;
			ELSE IF PEHSPNON = 2 & PTDTRACE = 2 THEN RACE = 2;
			ELSE IF PEHSPNON = 2 & PTDTRACE IN(3,4) THEN RACE = 3;
			ELSE IF PEHSPNON = 2 & PTDTRACE NOTIN(1:4) THEN RACE = 4;
			ELSE IF PEHSPNON = 1 THEN RACE = 5;
	END;

	/* Educational Attainment */
	EDUC = .;
	IF PEEDUCA IN(31:38) THEN EDUC = 1; /* Less than High School */
		ELSE IF PEEDUCA IN(39) THEN EDUC = 2; /* High School Graduate */
		ELSE IF PEEDUCA IN(40:42) THEN EDUC = 3; /* Some College Experience */
		ELSE IF PEEDUCA IN(43:46) THEN EDUC = 4; /* Bachelor's, Master's, or Professional Degree */

	/* Labor Force Status */
	IF PEMLR in(1,2) & (50 <= PEHRUSLT <= 198) THEN LFSTATUS = 1; /* Full-time */
		ELSE IF PEMLR in(1,2) & ((36 <= PEHRUSLT <= 49) | (PEHRFTPT = 1)) THEN LFSTATUS = 2; /* Full-time */
		ELSE IF PEMLR in(1,2) & ((0 <= PEHRUSLT <= 35) | (PEHRFTPT in(1,2))) THEN LFSTATUS = 3; /* Part-time */
		ELSE IF PEMLR in(3,4) THEN LFSTATUS = 4; /* Unemployed */
		ELSE IF PEMLR in(5,6,7) & PENLFACT = 4 THEN LFSTATUS = 5; /* Taking care of house/family */
		ELSE IF PEMLR in(5,6,7) & ((PESCHENR = 1) | (PEDWAVR = 2)) THEN LFSTATUS = 6; /* In School */
		ELSE IF PEMLR = 5 THEN LFSTATUS = 7; /* Retired */
		ELSE IF PEMLR = 6 THEN LFSTATUS = 9; /* With a disability */
		ELSE LFSTATUS = 8; /* NILF - Other reason */

	PWGT = PWSSWGT;
	MONNUM = INPUT(PUT(HRMONTH,z2.),$2.);
	
	IF PRTAGE IN(16:17) OR PEAGE IN(16:17) THEN AGECAT = 16;
		ELSE IF PRTAGE IN(18:24) OR PEAGE IN(18:24) THEN AGECAT = 18;
		ELSE IF PRTAGE IN(25:29) OR PEAGE IN(25:29) THEN AGECAT = 25;
		ELSE IF PRTAGE IN(30:34) OR PEAGE IN(30:34) THEN AGECAT = 30;
		ELSE IF PRTAGE IN(35:39) OR PEAGE IN(35:39) THEN AGECAT = 35;
		ELSE IF PRTAGE IN(40:44) OR PEAGE IN(40:44) THEN AGECAT = 40;
		ELSE IF PRTAGE IN(45:49) OR PEAGE IN(45:49) THEN AGECAT = 45;
		ELSE IF PRTAGE IN(50:54) OR PEAGE IN(50:54) THEN AGECAT = 50;
		ELSE IF PRTAGE IN(55:59) OR PEAGE IN(55:59) THEN AGECAT = 55;
		ELSE IF PRTAGE IN(60:64) OR PEAGE IN(60:64) THEN AGECAT = 60;
		ELSE IF PRTAGE IN(65:69) OR PEAGE IN(65:69) THEN AGECAT = 65;
		ELSE IF PRTAGE IN(70:74) OR PEAGE IN(70:74) THEN AGECAT = 70;
		ELSE IF PRTAGE IN(75:99) OR PEAGE IN(75:99) THEN AGECAT = 75;
	IF PRTAGE NE . THEN AGE = PRTAGE;
		ELSE IF PEAGE NE . THEN AGE = PEAGE;

	WHERE HRYEAR4 IN(2005:2015) & PULINENO NE -1;
	IF AGE IN(16:99);
RUN;

PROC FREQ DATA=OUTCOME.OUTCOME1(KEEP=KEYID HRYEAR4 HRMONTH HRMIS PUCHINHH) NOPRINT;
	TABLES KEYID / OUT=OUTCOME.KEYID(KEEP=KEYID);
	WHERE	(HRYEAR4 IN(2005:2014) & HRMIS = 1 & NOT (HRYEAR4 = 2014 & HRMONTH IN(10:12)))
		OR	(HRYEAR4 IN(2005:2014) & PUCHINHH IN(1,2) & HRMIS IN(2:4));
RUN;

DATA OUTCOME.OUTCOME2(DROP=rc);
	IF 0 THEN SET OUTCOME.KEYID OUTCOME.OUTCOME1;
	IF _n_ = 1 THEN DO;
		DECLARE HASH KEYHASH(DATASET:'OUTCOME.KEYID');
		KEYHASH.DEFINEKEY('KEYID');
		KEYHASH.DEFINEDATA(ALL:'YES');
		KEYHASH.DEFINEDONE();
	END;
	SET OUTCOME.OUTCOME1;
	IF KEYHASH.FIND(KEY:KEYID) = 0 THEN OUTPUT;

	WHERE AGE IN(16:99);
RUN;

PROC SORT DATA=OUTCOME.OUTCOME2 OUT=OUTCOME.OUTCOME3;
	BY KEYID DESCENDING HRMIS;
RUN;

DATA OUTCOME.OUTCOME4;
	SET OUTCOME.OUTCOME3;
	BY KEYID;
	RETAIN MIS1-MIS8 PEMLR1-PEMLR8;
	ARRAY MIS(8) MIS1-MIS8;
	ARRAY AR_STATUS(8) PEMLR1-PEMLR8;

	IF FIRST.KEYID THEN DO iter1 = 8 TO 1 BY -1;
		MIS(iter1) = "0";
		AR_STATUS(iter1) = .;
	END;

	DO iter2 = 8 to 1 BY -1;
		IF HRMIS = (iter2) THEN MIS(iter2) = "1";
		IF PEMLR NE -1 THEN DO;
			IF HRMIS = (iter2) THEN AR_STATUS(iter2) = PEMLR;
		END;
	END;

	ATTRIB COVERAGE FORMAT=$8.;
	COVERAGE = COMPRESS(MIS1||MIS2||MIS3||MIS4||MIS5||MIS6||MIS7||MIS8);
	INTERVIEWS = SUM(OF MIS1-MIS8);

	IF LAST.KEYID & INTERVIEWS >=4 & SUM(OF MIS1-MIS4) >= 1 & SUM(OF MIS5-MIS8) >= 1;
	DROP iter: MIS1-MIS8;
RUN;

DATA OUTCOME.OUTCOME5;
	SET OUTCOME.OUTCOME4;
	UE_OUT = .;	EMP_OUT = . ;
	MIS_START = .; PEM_START = .;
	ARRAY AR_STATUS(*) PEMLR1-PEMLR8;
	DO i1 = 1 TO 8 UNTIL(MIS_START NE .);
		IF AR_STATUS(i1) NOTIN(.,-1) & (i1) IN(1:8) THEN DO;
			MIS_START = (i1);
			PEM_START = AR_STATUS(i1);
		END;
	END;
	IF PEM_START IN(3,4) THEN DO;
		UE_OUT = 0;
		DO i2 = MIS_START + 1 TO DIM(AR_STATUS) UNTIL(UE_OUT NE 0);
			IF AR_STATUS(i2) IN(1,2) THEN UE_OUT = 1;
				ELSE IF AR_STATUS(i2) IN(5,6,7) THEN UE_OUT = 2;
		END;
	END;

	IF PEM_START IN(1,2) THEN DO;
		EMP_OUT = 0;
		DO i2 = MIS_START + 1 TO DIM(AR_STATUS) UNTIL(EMP_OUT NE 0);
			IF AR_STATUS(i2) IN(3,4) THEN EMP_OUT = 1;
				ELSE IF AR_STATUS(i2) IN(5,6,7) THEN EMP_OUT = 2;
		END;
	END;
RUN;

/* Section 4: Statistics */;
PROC FREQ DATA=OUTCOME.OUTCOME5;
	TABLE (EDUC RACE)*(UE_OUT EMP_OUT) / NOCOL NOFREQ NOCUM NOPERCENT;
	WEIGHT PWGT;
RUN;

/* Section 5: Output */;
PROC SUMMARY DATA=OUTCOME.OUTCOME5(RENAME=(HRYEAR4=YEAR PESEX=SEX)) NWAY MISSING;
	CLASS YEAR RACE EDUC SEX UE_OUT EMP_OUT;
	VAR PWGT;
	OUTPUT OUT=OUTCOME.OUTCOME_SUM(DROP=_TYPE_) SUM=POP;
	*WHERE YEAR = . OR RACE = . OR EDUC = . OR AGECAT = .;
	WHERE UE_OUT NE . OR EMP_OUT NE .;
RUN;
QUIT;

PROC EXPORT DATA=OUTCOME.OUTCOME_SUM OUTFILE="&OUTCOME.OUTCOME_SUM.CSV" DBMS=CSV REPLACE;
RUN;

DATA OUTCOME.RACECAT(DROP=FILLER);
	INPUT RACE 1. FILLER $1. RACECAT $18.;
CARDS;
1 Non-Hispanic White
2 Non-Hispanic Black
3 Non-Hispanic Asian
4 Non-Hispanic Other
5 Hispanic or Latino
;
RUN;

PROC EXPORT DATA=OUTCOME.RACECAT DBMS=CSV OUTFILE="&OUTCOME.RACECAT.CSV" REPLACE;
RUN;

DATA OUTCOME.AGECAT(DROP=FILLER);
	INPUT AGECAT 2. FILLER $1. RANGE $8.;
CARDS;
16 16 to 17
18 18 to 24
25 25 to 29
30 30 to 34
35 35 to 39
40 40 to 44
45 45 to 49
50 50 to 54
55 55 to 59
60 60 to 64
65 65 to 69
70 70 to 74
75 75 plus
;
RUN;

PROC EXPORT DATA=OUTCOME.AGECAT DBMS=CSV OUTFILE="&OUTCOME.AGECAT.CSV" REPLACE;
RUN;

DATA OUTCOME.EDUC(DROP=FILLER);
	INPUT EDUC 1. FILLER $1. EDUCCAT $21.;
CARDS;
1 Less than High School
2 High School
3 Some College
4 Bachelor's or Higher
;
RUN;

PROC EXPORT DATA=OUTCOME.EDUC DBMS=CSV OUTFILE="&OUTCOME.EDUC.CSV" REPLACE;
RUN;

DATA OUTCOME.SEX(DROP=FILLER);
	INPUT SEX 1. FILLER $1. SEXCAT $6.;
CARDS;
1 Male
2 Female
;
RUN;

PROC EXPORT DATA=OUTCOME.SEX DBMS=CSV OUTFILE="&OUTCOME.SEX.CSV" REPLACE;
RUN;
